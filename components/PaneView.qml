import QtQuick
import QtQuick.Controls
import Quickshell
import qs.Commons
import qs.Ui
import "../Model.js" as Model
import "../Icons.js" as Icons

Item {
  id: pane

  property var service: null
  property string path: ""
  property bool showHidden: false
  property string sortBy: "name"
  property bool descending: false
  property bool dirsFirst: true
  property string view: "list"
  property bool thumbnails: true
  property real viewScale: 1
  property var patterns: []

  readonly property int rowHeight: Math.round(Style.space(22) * viewScale)
  readonly property int listIconSize: Math.round(Style.space(18) * viewScale)
  readonly property int gridIconSize: Math.round(Style.space(48) * viewScale)
  readonly property bool compactView: view === "compact"

  function scaled(value) {
    return Math.max(1, Math.round(value * viewScale))
  }
  property bool active: false
  property string filter: ""

  property var entries: []
  property var rows: []
  property var selection: ({})
  property string dragImage: ""
  property int cursorIndex: -1
  property int anchorIndex: -1
  property bool loading: false
  property string errorMessage: ""
  property int total: 0

  property var history: []
  property int historyIndex: -1

  property bool searching: false
  property string searchQuery: ""
  property bool searchTruncated: false
  property int _searchId: 0
  property int _generation: 0
  readonly property bool virtualView: pane.path === "recent:"
  property int _listId: 0
  property int _watchId: 0
  property string _watchPath: ""
  property var _pendingChunks: []

  readonly property bool canGoBack: historyIndex > 0
  readonly property bool canGoForward: historyIndex >= 0 && historyIndex < history.length - 1
  readonly property int selectedCount: countSelection()
  readonly property var selectedEntries: collectSelected()

  signal activated()
  signal navigated(string newPath)
  signal openRequested(var entry)
  signal contextRequested(var entry, real sceneX, real sceneY)
  signal statusChanged()
  signal zoomRequested(real delta)
  signal filesDropped(var paths, string target, string mode)

  function countSelection() {
    var n = 0
    for (var k in selection) if (selection[k]) n++
    return n
  }

  function collectSelected() {
    var out = []
    for (var i = 0; i < rows.length; i++)
      if (selection[rows[i][0]]) out.push(Model.decodeEntry(rows[i], pane.path))
    return out
  }

  function selectedPaths() {
    var out = []
    for (var i = 0; i < rows.length; i++)
      if (selection[rows[i][0]]) out.push(Model.decodeEntry(rows[i], pane.path).path)
    return out
  }

  function activeView() {
    return pane.view === "list" ? listView : gridView
  }

  function columnsPerRow() {
    if (pane.view === "list" || gridView.cellWidth <= 0) return 1
    return Math.max(1, Math.floor(gridView.width / gridView.cellWidth))
  }

  function hitTestIndex(x, y) {
    var v = activeView()
    var pt = bandArea.mapToItem(v, x, y)
    if (pt.x < 0 || pt.y < 0 || pt.x > v.width || pt.y > v.height) return -1
    return v.indexAt(pt.x + v.contentX, pt.y + v.contentY)
  }

  function selectInBand(x1, y1, x2, y2, base) {
    var v = activeView()
    var a = bandArea.mapToItem(v, Math.min(x1, x2), Math.min(y1, y2))
    var b = bandArea.mapToItem(v, Math.max(x1, x2), Math.max(y1, y2))
    var left = a.x + v.contentX
    var top = a.y + v.contentY
    var right = b.x + v.contentX
    var bottom = b.y + v.contentY
    var next = {}
    for (var k in base) if (base[k]) next[k] = true
    if (pane.view === "list") {
      var h = pane.rowHeight
      if (h > 0) {
        var i0 = Math.max(0, Math.floor(top / h))
        var i1 = Math.min(pane.rows.length - 1, Math.floor(bottom / h))
        for (var i = i0; i <= i1; i++) next[pane.rows[i][0]] = true
      }
    } else {
      var cw = gridView.cellWidth
      var chh = gridView.cellHeight
      if (cw > 0 && chh > 0) {
        var cols = Math.max(1, Math.floor(gridView.width / cw))
        var c0 = Math.max(0, Math.floor(left / cw))
        var c1 = Math.min(cols - 1, Math.floor(right / cw))
        var r0 = Math.max(0, Math.floor(top / chh))
        var r1 = Math.floor(bottom / chh)
        for (var r = r0; r <= r1; r++) {
          for (var c = c0; c <= c1; c++) {
            var idx = r * cols + c
            if (idx >= 0 && idx < pane.rows.length) next[pane.rows[idx][0]] = true
          }
        }
      }
    }
    pane.selection = next
    pane.statusChanged()
  }

  function cursorEntry() {
    if (cursorIndex < 0 || cursorIndex >= rows.length) return null
    return Model.decodeEntry(rows[cursorIndex], pane.path)
  }

  function navigate(target, recordHistory) {
    var raw = String(target || "")
    var next = raw === "recent:"
      ? raw
      : Model.normalizePath(Model.expandTilde(raw, Quickshell.env("HOME") || ""))
    if (!next) return
    if (pane.searching) {
      if (_searchId && service) service.cancel(_searchId)
      _searchId = 0
      pane.searching = false
      pane.searchQuery = ""
    }
    if (recordHistory !== false) pushHistory(next)
    pane.path = next
    reload()
    pane.navigated(next)
    if (service) service.noteRecent(next)
  }

  function pushHistory(next) {
    var trimmed = history.slice(0, historyIndex + 1)
    if (trimmed.length === 0 || trimmed[trimmed.length - 1] !== next) trimmed.push(next)
    if (trimmed.length > 100) trimmed = trimmed.slice(trimmed.length - 100)
    history = trimmed
    historyIndex = trimmed.length - 1
  }

  function goBack() {
    if (!canGoBack) return
    historyIndex = historyIndex - 1
    pane.path = history[historyIndex]
    reload()
    pane.navigated(pane.path)
  }

  function goForward() {
    if (!canGoForward) return
    historyIndex = historyIndex + 1
    pane.path = history[historyIndex]
    reload()
    pane.navigated(pane.path)
  }

  function goUp() {
    if (pane.virtualView) return
    var parent = Model.parentPath(pane.path)
    if (parent === pane.path) return
    var leaving = Model.basename(pane.path)
    navigate(parent)
    Qt.callLater(function () { focusName(leaving) })
  }

  function focusName(name) {
    for (var i = 0; i < rows.length; i++) {
      if (rows[i][0] === name) {
        setCursor(i, false, false)
        activeView().positionViewAtIndex(i, ListView.Contain)
        return
      }
    }
  }

  function hitToRow(hit) {
    return [
      String(hit.name || ""), String(hit.kind || "f"),
      Number(hit.size) || 0, Number(hit.mtime) || 0,
      Number(hit.mode) || 0, null, String(hit.path || "")
    ]
  }

  function startSearch(query) {
    if (!service || !pane.path) return
    var trimmed = String(query || "").trim()
    pane.searchQuery = trimmed
    if (!trimmed) {
      stopSearch()
      return
    }
    if (_searchId) service.cancel(_searchId)
    if (_listId) service.cancel(_listId)
    pane.searching = true
    pane.searchTruncated = false
    pane.errorMessage = ""
    pane.loading = true
    pane.entries = []
    pane.rows = []
    pane.selection = ({})
    pane.cursorIndex = -1
    pane._pendingChunks = []

    var searchGeneration = ++pane._generation

    _searchId = service.searchFiles(pane.path, trimmed, "substring", pane.showHidden,
      function (hit) {
        if (searchGeneration !== pane._generation) return
        pane._pendingChunks.push(pane.hitToRow(hit))
        if (!rebuildTimer.running) rebuildTimer.start()
      },
      function (msg) {
        if (searchGeneration !== pane._generation) return
        pane.loading = false
        pane.searchTruncated = msg.truncated === true
        pane.flushChunks()
        pane.statusChanged()
      },
      function (msg) {
        if (searchGeneration !== pane._generation) return
        pane.loading = false
        if (msg.code !== "ECANCELED")
          pane.errorMessage = String(msg.message || "Search failed")
        pane.statusChanged()
      })
  }

  function stopSearch() {
    if (_searchId && service) service.cancel(_searchId)
    _searchId = 0
    pane.searchQuery = ""
    pane.searchTruncated = false
    if (pane.searching) {
      pane.searching = false
      reload()
    }
  }

  function loadRecent() {
    if (!service) return
    if (_listId) service.cancel(_listId)
    var generation = ++pane._generation
    _pendingChunks = []
    entries = []
    rows = []
    selection = ({})
    cursorIndex = -1
    anchorIndex = -1
    errorMessage = ""
    loading = true
    total = 0
    rebuildTimer.stop()

    if (_watchId) {
      service.unwatch(_watchId, _watchPath)
      _watchId = 0
      _watchPath = ""
    }

    _listId = service.listRecent(
      function (chunk) {
        if (generation !== pane._generation) return
        var acc = pane._pendingChunks
        for (var i = 0; i < chunk.length; i++) acc.push(chunk[i])
        if (!rebuildTimer.running) rebuildTimer.start()
      },
      function (msg) {
        if (generation !== pane._generation) return
        pane.loading = false
        pane.total = Number(msg.total) || 0
        pane.flushChunks()
        pane.statusChanged()
      },
      function (msg) {
        if (generation !== pane._generation) return
        pane.loading = false
        if (String(msg.code || "") !== "ECANCELED")
          pane.errorMessage = String(msg.message || "Could not read recent files")
        pane.statusChanged()
      })
  }

  function reload() {
    if (!service || !pane.path) return
    if (pane.virtualView) {
      loadRecent()
      return
    }
    if (pane.searching) return
    if (_listId) service.cancel(_listId)
    _pendingChunks = []
    entries = []
    rows = []
    selection = ({})
    cursorIndex = -1
    anchorIndex = -1
    errorMessage = ""
    loading = true
    total = 0
    rebuildTimer.stop()

    var generation = ++pane._generation

    _listId = service.listDirectory(pane.path, pane.showHidden,
      function (chunk) {
        if (generation !== pane._generation) return
        var acc = pane._pendingChunks
        for (var i = 0; i < chunk.length; i++) acc.push(chunk[i])
        if (!rebuildTimer.running) rebuildTimer.start()
      },
      function (msg) {
        if (generation !== pane._generation) return
        pane.loading = false
        pane.total = Number(msg.total) || pane._pendingChunks.length
        pane.flushChunks()
        pane.statusChanged()
      },
      function (msg) {
        if (generation !== pane._generation) return
        if (String(msg.code || "") === "ECANCELED") return
        pane.loading = false
        pane.errorMessage = String(msg.message || msg.code || "Cannot open this folder")
        pane.statusChanged()
      })

    rewatch()
  }

  function flushChunks() {
    if (_pendingChunks.length === 0 && entries.length === 0) {
      rows = []
      return
    }
    if (_pendingChunks.length > 0) {
      entries = entries.concat(_pendingChunks)
      _pendingChunks = []
    }
    if (loading) {
      rows = Model.filterByPatterns(pane.filter ? Model.filterRaw(entries, pane.filter) : entries, pane.patterns)
      statusChanged()
      return
    }
    rebuild()
  }

  function rebuild() {
    var filtered = Model.filterByPatterns(((pane.searching || pane.virtualView) || !pane.filter)
      ? entries : Model.filterRaw(entries, pane.filter), pane.patterns)
    if (pane.virtualView
        || (!pane.searching && Model.isDefaultOrder(pane.sortBy, pane.descending, pane.dirsFirst)))
      rows = filtered
    else
      rows = Model.sortRaw(filtered, pane.sortBy, pane.descending, pane.dirsFirst)
    if (cursorIndex >= rows.length) cursorIndex = rows.length - 1
    statusChanged()
  }

  function rewatch() {
    if (!service) return
    if (_watchId) service.unwatch(_watchId, _watchPath)
    _watchPath = pane.path
    _watchId = service.watchDirectory(pane.path, function () { refreshTimer.restart() })
  }

  function refresh() {
    var keepSelection = ({})
    for (var k in selection) keepSelection[k] = selection[k]
    var keepCursor = cursorIndex
    var restore = function () {
      pane.selection = keepSelection
      if (keepCursor >= 0 && keepCursor < pane.rows.length) pane.cursorIndex = keepCursor
    }
    reload()
    Qt.callLater(restore)
  }

  function setCursor(index, extend, toggle) {
    if (index < 0 || index >= rows.length) return
    cursorIndex = index
    if (toggle) {
      var name = rows[index][0]
      var next = ({})
      for (var k in selection) next[k] = selection[k]
      next[name] = !next[name]
      selection = next
      anchorIndex = index
      return
    }
    if (extend && anchorIndex >= 0) {
      var lo = Math.min(anchorIndex, index)
      var hi = Math.max(anchorIndex, index)
      var range = ({})
      for (var i = lo; i <= hi; i++) range[rows[i][0]] = true
      selection = range
      return
    }
    var single = ({})
    single[rows[index][0]] = true
    selection = single
    anchorIndex = index
  }

  function toggleCursorSelection() {
    if (cursorIndex < 0 || cursorIndex >= rows.length) return
    var name = rows[cursorIndex][0]
    var next = ({})
    for (var k in selection) next[k] = selection[k]
    next[name] = !next[name]
    selection = next
    anchorIndex = cursorIndex
  }

  function invertSelection() {
    var next = ({})
    for (var i = 0; i < rows.length; i++) {
      var name = rows[i][0]
      if (!selection[name]) next[name] = true
    }
    selection = next
  }

  function selectAll() {
    var all = ({})
    for (var i = 0; i < rows.length; i++) all[rows[i][0]] = true
    selection = all
  }

  function clearSelection() {
    selection = ({})
  }

  function jumpCursor(index, extend) {
    if (rows.length === 0) return
    var target = Math.max(0, Math.min(rows.length - 1, index))
    setCursor(target, extend, false)
    activeView().positionViewAtIndex(target, ListView.Contain)
  }

  function moveCursor(delta, extend) {
    if (rows.length === 0) return
    var next = cursorIndex < 0 ? 0 : cursorIndex + delta
    next = Math.max(0, Math.min(rows.length - 1, next))
    setCursor(next, extend, false)
    activeView().positionViewAtIndex(next, ListView.Contain)
  }

  function openEntry(entry) {
    if (!entry) return
    if (entry.isDir && !entry.isBroken) navigate(entry.path)
    else pane.openRequested(entry)
  }

  function gridLabel(name) {
    var text = String(name || "")
    if (text.length <= 26) return text
    return text.substring(0, 14) + "\u2026" + text.substring(text.length - 10)
  }

  function previewable(entry) {
    if (!pane.thumbnails) return false
    if (entry.isDir || entry.isBroken) return false
    if (entry.size <= 0 || entry.size > 24000000) return false
    var e = entry.ext
    return e === "png" || e === "jpg" || e === "jpeg" || e === "gif"
      || e === "webp" || e === "bmp" || e === "svg" || e === "ico" || e === "avif"
  }

  function pressKeepsSelection(index, extend, toggle) {
    if (extend || toggle || index < 0 || index >= rows.length) return false
    return selection[rows[index][0]] === true && selectedCount > 1
  }

  function pastDragThreshold(dx, dy) {
    return Math.abs(dx) + Math.abs(dy) >= Style.space(10)
  }

  function prepareDragImage(item) {
    pane.dragImage = ""
    if (!item || item.width <= 0 || item.height <= 0) return
    item.grabToImage(function (result) { pane.dragImage = String(result.url) },
      Qt.size(Math.round(item.width * 2), Math.round(item.height * 2)))
  }

  function dragMimeData(paths) {
    return { "text/uri-list": Model.uriList(paths) }
  }

  function startDrag(index) {
    if (index < 0 || index >= rows.length || dragSource.Drag.active) return
    if (!selection[rows[index][0]]) setCursor(index, false, false)
    var paths = selectedPaths()
    if (paths.length === 0) return
    dragSource.paths = paths
    dragSource.Drag.mimeData = dragMimeData(paths)
    dragSource.Drag.imageSource = pane.dragImage
    dragSource.Drag.hotSpot = Qt.point(Style.space(8), Style.space(8))
    if (service) service.dragPaths = paths
    dragSource.Drag.active = true
  }

  function dropPaths(event) {
    return event && event.hasUrls ? Model.localPathsFromUrls(event.urls) : []
  }

  function acceptsDrop(drag, target) {
    var paths = dropPaths(drag)
    if (paths.length === 0) return false
    return Model.dropSources(paths, target, drag.proposedAction === Qt.CopyAction).length > 0
  }

  function handleDrop(drop, target) {
    var paths = dropPaths(drop)
    if (paths.length === 0) return
    var internal = (drop.source !== null && drop.source !== undefined && drop.source.omafileDrag === true)
      || Model.samePaths(paths, service ? service.dragPaths : [])
    var mode = !internal || drop.proposedAction === Qt.CopyAction ? "copy" : "auto"
    drop.accept(Qt.CopyAction)
    pane.filesDropped(paths, target, mode)
  }


  function thumbable(entry) {
    if (!pane.thumbnails || !pane.service || !pane.service.thumbExts) return false
    if (entry.isDir || entry.isBroken || entry.size <= 0) return false
    return pane.service.thumbExts[entry.ext] === true
  }

  function openRow(row) {
    openEntry(Model.decodeEntry(row, pane.path))
  }

  function activateCursor() {
    openEntry(cursorEntry())
  }

  function setSort(column) {
    if (pane.virtualView) return
    if (pane.sortBy === column) pane.descending = !pane.descending
    else {
      pane.sortBy = column
      pane.descending = false
    }
    rebuild()
  }

  function setSortOrder(column, descending) {
    if (pane.virtualView) return
    pane.sortBy = column
    pane.descending = descending === true
    rebuild()
  }

  property bool ready: true

  onServiceChanged: {
    if (service && pane.path && !loading && rows.length === 0) reload()
  }

  onShowHiddenChanged: if (ready) reload()
  onFilterChanged: if (ready) rebuild()
  onPatternsChanged: if (ready) rebuild()
  onDirsFirstChanged: if (ready) rebuild()

  Component.onDestruction: {
    if (service && _watchId) service.unwatch(_watchId, _watchPath)
    if (service && _listId) service.cancel(_listId)
    if (service && _searchId) service.cancel(_searchId)
  }

  Timer {
    id: rebuildTimer
    interval: 120
    repeat: false
    onTriggered: pane.flushChunks()
  }

  Timer {
    id: stallWatchdog
    interval: 6000
    repeat: false
    running: pane.loading && pane.rows.length === 0 && pane.path !== ""
    onTriggered: {
      if (!pane.loading || pane.rows.length > 0) return
      if (!pane.service) return
      if (pane.searching) return
      pane.loading = false
      pane.reload()
    }
  }

  Timer {
    id: refreshTimer
    interval: 180
    repeat: false
    onTriggered: pane.refresh()
  }

  readonly property color fg: Color.foreground
  readonly property color bg: Color.background
  readonly property color accent: Color.accent
  readonly property color muted: Color.muted

  Rectangle {
    anchors.fill: parent
    color: pane.bg
    border.width: Math.max(1, Style.space(1))
    border.color: paneDrop.containsDrag ? pane.accent
      : (pane.active ? Util.alpha(pane.accent, 0.5) : Util.alpha(pane.fg, 0.15))

    DropArea {
      id: paneDrop
      objectName: "paneDrop"
      anchors.fill: parent
      enabled: !pane.virtualView && pane.path !== ""
      keys: ["text/uri-list"]
      onEntered: function (drag) { if (!pane.acceptsDrop(drag, pane.path)) drag.accepted = false }
      onDropped: function (drop) { pane.handleDrop(drop, pane.path) }
    }

    Item {
      id: dragSource
      objectName: "dragSource"
      readonly property bool omafileDrag: true
      property var paths: []
      width: 1
      height: 1
      Drag.dragType: Drag.Automatic
      Drag.source: dragSource
      Drag.supportedActions: Qt.CopyAction | Qt.MoveAction | Qt.LinkAction
      Drag.proposedAction: Qt.MoveAction
      Drag.onDragFinished: function (dropAction) {
        if (pane.service) pane.service.dragPaths = []
      }
    }

    MouseArea {
      id: bandArea
      anchors.fill: parent
      z: 10
      acceptedButtons: Qt.LeftButton | Qt.RightButton

      property real originX: 0
      property real originY: 0
      property real currentX: 0
      property real currentY: 0
      property bool banding: false
      property var baseSelection: ({})

      onPressed: function (mouse) {
        pane.activated()
        if (pane.view === "list" && mouse.y < header.height + Style.space(1)) {
          mouse.accepted = false
          return
        }
        if (pane.hitTestIndex(mouse.x, mouse.y) >= 0) {
          mouse.accepted = false
          return
        }
        if (mouse.button === Qt.RightButton) {
          pane.clearSelection()
          pane.contextRequested(null, mouse.x, mouse.y)
          return
        }
        var additive = (mouse.modifiers & Qt.ControlModifier) !== 0
        baseSelection = additive ? pane.selection : ({})
        if (!additive) pane.clearSelection()
        originX = mouse.x
        originY = mouse.y
        currentX = mouse.x
        currentY = mouse.y
        banding = true
      }

      onPositionChanged: function (mouse) {
        if (!banding) return
        currentX = mouse.x
        currentY = mouse.y
        pane.selectInBand(originX, originY, currentX, currentY, baseSelection)
      }

      onReleased: banding = false
      onCanceled: banding = false

      property real wheelAccum: 0

      onWheel: function (wheel) {
        if (!(wheel.modifiers & Qt.ControlModifier)) {
          wheel.accepted = false
          return
        }
        wheelAccum += wheel.angleDelta.y
        var steps = wheelAccum > 0 ? Math.floor(wheelAccum / 120) : Math.ceil(wheelAccum / 120)
        if (steps !== 0) {
          wheelAccum -= steps * 120
          pane.zoomRequested(steps * 0.1)
        }
      }

      Rectangle {
        visible: bandArea.banding
        x: Math.min(bandArea.originX, bandArea.currentX)
        y: Math.min(bandArea.originY, bandArea.currentY)
        width: Math.abs(bandArea.currentX - bandArea.originX)
        height: Math.abs(bandArea.currentY - bandArea.originY)
        color: Util.alpha(pane.accent, 0.15)
        border.width: 1
        border.color: Util.alpha(pane.accent, 0.6)
      }
    }

    Column {
      anchors.fill: parent
      anchors.margins: Style.space(1)
      spacing: 0

      Row {
        id: header
        width: parent.width
        height: pane.view === "list" ? Style.space(22) : 0
        visible: pane.view === "list"
        spacing: 0

        Repeater {
          model: [
            { key: "name", label: "Name", weight: 0.52 },
            { key: "size", label: "Size", weight: 0.14 },
            { key: "type", label: "Type", weight: 0.16 },
            { key: "modified", label: "Modified", weight: 0.18 }
          ]

          delegate: Item {
            required property var modelData
            objectName: "header-" + modelData.key
            width: header.width * modelData.weight
            height: header.height

            Text {
              anchors.left: parent.left
              anchors.right: parent.right
              anchors.verticalCenter: parent.verticalCenter
              anchors.leftMargin: Style.space(8)
              anchors.rightMargin: Style.space(8)
              text: modelData.label + ((pane.sortBy === modelData.key && !pane.virtualView)
                ? (pane.descending ? "  " + Icons.actionGlyph("chevronDown")
                  : "  " + Icons.actionGlyph("chevronUp")) : "")
              color: pane.sortBy === modelData.key ? pane.accent : Util.alpha(pane.fg, 0.6)
              font.family: Style.font.family
              font.pixelSize: Style.font.caption
              elide: Text.ElideRight
            }

            MouseArea {
              anchors.fill: parent
              onClicked: pane.setSort(modelData.key)
            }
          }
        }
      }

      Rectangle {
        width: parent.width
        height: pane.view === "list" ? 1 : 0
        visible: pane.view === "list"
        color: Util.alpha(pane.fg, 0.12)
      }

      ListView {
        id: listView
        width: parent.width
        height: parent.height - header.height - (pane.view === "list" ? 1 : 0)
        clip: true
        model: pane.rows
        cacheBuffer: 400
        boundsBehavior: Flickable.StopAtBounds
        currentIndex: pane.cursorIndex
        visible: pane.view === "list"

        ScrollBar.vertical: ScrollBar { policy: ScrollBar.AsNeeded }

        delegate: Rectangle {
          id: row
          required property var modelData
          required property int index

          readonly property var entry: Model.decodeEntry(modelData, pane.path)

          width: listView.width
          height: pane.rowHeight
          color: rowDrop.containsDrag
            ? Util.alpha(pane.accent, 0.3)
            : (pane.selection[modelData[0]]
              ? Util.alpha(pane.accent, Style.selectedFillAlpha)
              : (rowHover.hovered ? Util.alpha(pane.fg, Style.hoverFillAlpha) : "transparent"))

          Rectangle {
            anchors.fill: parent
            color: "transparent"
            border.width: pane.cursorIndex === index && pane.active ? 1 : 0
            border.color: Util.alpha(pane.accent, 0.8)
          }

          HoverHandler { id: rowHover }

          DropArea {
            id: rowDrop
            anchors.fill: parent
            enabled: row.entry.isDir && !row.entry.isBroken
            keys: ["text/uri-list"]
            onEntered: function (drag) { if (!pane.acceptsDrop(drag, row.entry.path)) drag.accepted = false }
            onDropped: function (drop) { pane.handleDrop(drop, row.entry.path) }
          }

          MouseArea {
            id: rowMouse
            anchors.fill: parent
            acceptedButtons: Qt.LeftButton | Qt.RightButton | Qt.MiddleButton
            preventStealing: true
            property real pressX: 0
            property real pressY: 0
            property bool dragReady: false
            property bool collapseOnRelease: false
            onPressed: function (mouse) {
              pane.activated()
              if (mouse.button === Qt.RightButton) {
                if (!pane.selection[row.modelData[0]]) pane.setCursor(row.index, false, false)
                pane.contextRequested(row.entry, mouse.x + row.x, mouse.y + row.y)
                return
              }
              var extend = (mouse.modifiers & Qt.ShiftModifier) !== 0
              var toggle = (mouse.modifiers & Qt.ControlModifier) !== 0
              pressX = mouse.x
              pressY = mouse.y
              dragReady = mouse.button === Qt.LeftButton
              collapseOnRelease = pane.pressKeepsSelection(row.index, extend, toggle)
              if (collapseOnRelease) pane.cursorIndex = row.index
              else pane.setCursor(row.index, extend, toggle)
              if (dragReady) pane.prepareDragImage(rowIcon)
            }
            onPositionChanged: function (mouse) {
              if (!dragReady || !pane.pastDragThreshold(mouse.x - pressX, mouse.y - pressY)) return
              dragReady = false
              collapseOnRelease = false
              pane.startDrag(row.index)
            }
            onReleased: {
              dragReady = false
              if (collapseOnRelease) pane.setCursor(row.index, false, false)
              collapseOnRelease = false
            }
            onCanceled: {
              dragReady = false
              collapseOnRelease = false
            }
            onDoubleClicked: function (mouse) {
              if (mouse.button !== Qt.LeftButton) return
              pane.openEntry(row.entry)
            }
          }

          Row {
            anchors.fill: parent
            spacing: 0

            Item {
              width: header.width * 0.52
              height: parent.height

              Row {
                anchors.left: parent.left
                anchors.right: parent.right
                anchors.verticalCenter: parent.verticalCenter
                anchors.leftMargin: Style.space(8)
                anchors.rightMargin: Style.space(8)
                spacing: Style.space(8)

                Item {
                  id: rowIcon
                  anchors.verticalCenter: parent.verticalCenter
                  width: Style.space(18)
                  height: pane.listIconSize

                  Text {
                    anchors.centerIn: parent
                    visible: !rowThumb.visible
                    text: Icons.glyphFor(row.entry)
                    color: row.entry.isBroken ? Color.urgent
                      : (row.entry.isDir ? pane.accent : Util.alpha(pane.fg, 0.75))
                    font.family: Style.font.family
                    font.pixelSize: pane.scaled(Style.font.icon)
                  }

                  ThumbImage {
                    id: rowThumb
                    anchors.fill: parent
                    service: pane.service
                    entry: row.entry
                    direct: pane.previewable(row.entry)
                    generated: pane.thumbable(row.entry)
                    requestSize: pane.listIconSize * 2
                    sourceSize.width: Style.space(36)
                    sourceSize.height: pane.listIconSize * 2
                    fillMode: Image.PreserveAspectFit
                    asynchronous: true
                    cache: true
                    smooth: true
                    mipmap: true
                  }
                }

                Text {
                  anchors.verticalCenter: parent.verticalCenter
                  width: parent.width - Style.space(28)
                  text: row.entry.name
                  color: row.entry.isHidden ? Util.alpha(pane.fg, 0.55) : pane.fg
                  font.family: Style.font.family
                  font.pixelSize: pane.scaled(Style.font.body)
                  font.italic: row.entry.isLink
                  elide: Text.ElideMiddle
                }
              }
            }

            Text {
              width: header.width * 0.14
              height: parent.height
              verticalAlignment: Text.AlignVCenter
              horizontalAlignment: Text.AlignRight
              rightPadding: Style.space(10)
              text: row.entry.isDir ? "" : Model.formatSize(row.entry.size)
              color: Util.alpha(pane.fg, 0.7)
              font.family: Style.font.family
              font.pixelSize: pane.scaled(Style.font.bodySmall)
            }

            Text {
              width: header.width * 0.16
              height: parent.height
              verticalAlignment: Text.AlignVCenter
              leftPadding: Style.space(10)
              text: Model.kindLabel(row.entry)
              color: Util.alpha(pane.fg, 0.55)
              font.family: Style.font.family
              font.pixelSize: pane.scaled(Style.font.bodySmall)
              elide: Text.ElideRight
            }

            Text {
              width: header.width * 0.18
              height: parent.height
              verticalAlignment: Text.AlignVCenter
              leftPadding: Style.space(10)
              text: Model.formatDate(row.entry.mtime, Date.now())
              color: Util.alpha(pane.fg, 0.55)
              font.family: Style.font.family
              font.pixelSize: pane.scaled(Style.font.bodySmall)
              elide: Text.ElideRight
            }
          }
        }
      }

      GridView {
        id: gridView
        width: parent.width
        height: parent.height - header.height
        clip: true
        model: pane.rows
        visible: pane.view !== "list"
        cellWidth: pane.compactView ? Math.round(Style.space(230) * pane.viewScale)
          : Math.round(Style.space(110) * pane.viewScale)
        cellHeight: pane.compactView ? pane.rowHeight + Style.space(2)
          : Math.round(Style.space(pane.view === "gallery" ? 196 : 96) * pane.viewScale)
        cacheBuffer: 600
        boundsBehavior: Flickable.StopAtBounds

        ScrollBar.vertical: ScrollBar { policy: ScrollBar.AsNeeded }

        delegate: Rectangle {
          id: cell
          required property var modelData
          required property int index

          readonly property var entry: Model.decodeEntry(modelData, pane.path)

          width: gridView.cellWidth - (pane.compactView ? Style.space(4) : 0)
          height: gridView.cellHeight
          radius: Style.cornerRadius
          color: pane.selection[modelData[0]]
            ? Util.alpha(pane.accent, Style.selectedFillAlpha)
            : (cellHover.hovered ? Util.alpha(pane.fg, Style.hoverFillAlpha) : "transparent")
          border.width: pane.cursorIndex === index && pane.active ? 1 : 0
          border.color: Util.alpha(pane.accent, 0.8)

          HoverHandler { id: cellHover }

          DropArea {
            id: cellDrop
            anchors.fill: parent
            enabled: cell.entry.isDir && !cell.entry.isBroken
            keys: ["text/uri-list"]
            onEntered: function (drag) { if (!pane.acceptsDrop(drag, cell.entry.path)) drag.accepted = false }
            onDropped: function (drop) { pane.handleDrop(drop, cell.entry.path) }
          }

          MouseArea {
            id: cellMouse
            anchors.fill: parent
            acceptedButtons: Qt.LeftButton | Qt.RightButton
            preventStealing: true
            property real pressX: 0
            property real pressY: 0
            property bool dragReady: false
            property bool collapseOnRelease: false
            onPressed: function (mouse) {
              pane.activated()
              if (mouse.button === Qt.RightButton) {
                if (!pane.selection[cell.modelData[0]]) pane.setCursor(cell.index, false, false)
                pane.contextRequested(cell.entry, mouse.x + cell.x, mouse.y + cell.y)
                return
              }
              var extend = (mouse.modifiers & Qt.ShiftModifier) !== 0
              var toggle = (mouse.modifiers & Qt.ControlModifier) !== 0
              pressX = mouse.x
              pressY = mouse.y
              dragReady = true
              collapseOnRelease = pane.pressKeepsSelection(cell.index, extend, toggle)
              if (collapseOnRelease) pane.cursorIndex = cell.index
              else pane.setCursor(cell.index, extend, toggle)
              pane.prepareDragImage(pane.compactView ? compactIcon : gridIcon)
            }
            onPositionChanged: function (mouse) {
              if (!dragReady || !pane.pastDragThreshold(mouse.x - pressX, mouse.y - pressY)) return
              dragReady = false
              collapseOnRelease = false
              pane.startDrag(cell.index)
            }
            onReleased: {
              dragReady = false
              if (collapseOnRelease) pane.setCursor(cell.index, false, false)
              collapseOnRelease = false
            }
            onCanceled: {
              dragReady = false
              collapseOnRelease = false
            }
            onDoubleClicked: pane.openEntry(cell.entry)
          }

          Row {
            anchors.fill: parent
            anchors.leftMargin: Style.space(8)
            anchors.rightMargin: Style.space(8)
            spacing: Style.space(8)
            visible: pane.compactView

            Item {
              id: compactIcon
              anchors.verticalCenter: parent.verticalCenter
              width: Style.space(18)
              height: pane.listIconSize

              Text {
                anchors.centerIn: parent
                visible: !compactThumb.visible
                text: Icons.glyphFor(cell.entry)
                color: cell.entry.isBroken ? Color.urgent
                  : (cell.entry.isDir ? pane.accent : Util.alpha(pane.fg, 0.75))
                font.family: Style.font.family
                font.pixelSize: pane.scaled(Style.font.icon)
              }

              ThumbImage {
                id: compactThumb
                anchors.fill: parent
                active: pane.compactView
                service: pane.service
                entry: cell.entry
                direct: pane.previewable(cell.entry)
                generated: pane.thumbable(cell.entry)
                requestSize: pane.listIconSize * 2
                sourceSize.width: Style.space(36)
                sourceSize.height: pane.listIconSize * 2
                fillMode: Image.PreserveAspectFit
                asynchronous: true
                cache: true
                smooth: true
                mipmap: true
              }
            }

            Text {
              anchors.verticalCenter: parent.verticalCenter
              width: parent.width - Style.space(26)
              text: cell.entry.name
              color: cell.entry.isHidden ? Util.alpha(pane.fg, 0.55) : pane.fg
              font.family: Style.font.family
              font.pixelSize: pane.scaled(Style.font.bodySmall)
              font.italic: cell.entry.isLink
              elide: Text.ElideMiddle
            }
          }

          Column {
            anchors.centerIn: parent
            width: parent.width - Style.space(12)
            spacing: Style.space(6)
            visible: !pane.compactView

            Item {
              id: gridIcon
              anchors.horizontalCenter: parent.horizontalCenter
              width: pane.gridIconSize
              height: pane.gridIconSize

              Text {
                anchors.centerIn: parent
                visible: !thumb.visible
                text: Icons.glyphFor(cell.entry)
                color: cell.entry.isBroken ? Color.urgent
                  : (cell.entry.isDir ? pane.accent : Util.alpha(pane.fg, 0.8))
                font.family: Style.font.family
                font.pixelSize: pane.scaled(Style.font.displayLarge)
              }

              ThumbImage {
                id: thumb
                anchors.centerIn: parent
                width: parent.width
                height: parent.height
                active: !pane.compactView
                service: pane.service
                entry: cell.entry
                direct: pane.previewable(cell.entry)
                generated: pane.thumbable(cell.entry)
                requestSize: pane.gridIconSize * 2
                sourceSize.width: pane.gridIconSize * 2
                sourceSize.height: pane.gridIconSize * 2
                fillMode: Image.PreserveAspectFit
                asynchronous: true
                cache: true
                smooth: true
                mipmap: true
              }
            }

            Text {
              width: parent.width
              horizontalAlignment: Text.AlignHCenter
              text: pane.view === "gallery" ? cell.entry.name : pane.gridLabel(cell.entry.name)
              color: pane.fg
              font.family: Style.font.family
              font.pixelSize: pane.scaled(Style.font.caption)
              maximumLineCount: 2
              wrapMode: Text.WrapAnywhere
            }
          }
        }
      }
    }

    Text {
      anchors.centerIn: parent
      visible: pane.errorMessage !== "" && pane.rows.length === 0
      width: parent.width - Style.space(40)
      horizontalAlignment: Text.AlignHCenter
      text: pane.errorMessage
      color: Color.urgent
      font.family: Style.font.family
      font.pixelSize: Style.font.body
      wrapMode: Text.Wrap
    }

    Text {
      anchors.centerIn: parent
      visible: !pane.loading && pane.errorMessage === "" && pane.rows.length === 0
      text: pane.searching ? "No matches"
        : (pane.virtualView ? "Nothing opened recently"
          : (pane.filter ? "Nothing matches" : "Empty folder"))
      color: Util.alpha(pane.fg, 0.45)
      font.family: Style.font.family
      font.pixelSize: Style.font.body
    }

    Text {
      anchors.centerIn: parent
      visible: pane.loading && pane.rows.length === 0
      text: pane.searching ? "Searching" : "Reading"
      color: Util.alpha(pane.fg, 0.45)
      font.family: Style.font.family
      font.pixelSize: Style.font.body
    }
  }
}
